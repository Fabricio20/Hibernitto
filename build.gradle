plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

group 'net.notfab.lib'
version '1.1.1'
description 'Persistence Engine'

sourceCompatibility = targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    repositories {
        maven {
            url = "/repository/Hosted"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId group
            artifactId rootProject.name
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

repositories {
    mavenLocal()
    maven { url "https://repo.maven.apache.org/maven2" }
    maven { url "https://maven.notfab.net/Hosted" }
}

dependencies {
    compileOnly group: 'org.hibernate', name: 'hibernate-hikaricp', version:'5.2.17.Final'

    // Configuration
    compileOnly group: 'net.notfab.spigot', name: 'SimpleConfig', version: '1.4.1'
}